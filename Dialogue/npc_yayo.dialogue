~ start
Avi: Hola Mila, fa un dia molt bonic, veritat?
do NPCManager.npc_states["abuelo"]["next_dialogue"] = "start_bloqueado"
do GameState._on_dialogue_ended()
=> END

~ start_bloqueado
Avi: ...
do GameState._on_dialogue_ended()
=> END

~ start_rio
Avi: Hola Mila
- Què fas al riu?
	Avi: No res.
- Desde quan vens al riu?
	Avi: Un dies, o una setmana ja?
do NPCManager.npc_states["abuelo"]["next_dialogue"] = "start_rio_2"
do GameState._on_dialogue_ended()
=> END

~ start_rio_2
Avi: Al principi tenia por de relliscar, però ja no.
do NPCManager.npc_states["abuelo"]["next_dialogue"] = "start_rio_3"
do GameState._on_dialogue_ended()
=> END

~ start_rio_3
Avi: El riu és hipnòtic, veritat?
do NPCManager.npc_states["abuelo"]["next_dialogue"] = "start_rio_4"
do GameState._on_dialogue_ended()
=> END 

~ start_rio_4
if NPCManager.npc_states["abuelo"]["amount_talk_river"] == 0:
	Avi: Estic bé, no necessito res.
elif NPCManager.npc_states["abuelo"]["amount_talk_river"] == 1:
	Avi: De veritat que estic bé.
elif NPCManager.npc_states["abuelo"]["amount_talk_river"] == 2:
	Avi: ...
do NPCManager.npc_states["abuelo"]["amount_talk_river"] += 1
if NPCManager.npc_states["abuelo"]["amount_talk_river"] >= 3:
	=> start_rio_5
else:
	do GameState._on_dialogue_ended()
	=> END

~ start_rio_5
Avi: Estic aqui per mon germà.
Avi: Va morir ofegat quan erem petits, en aquest riu. Desde llavors li he tingut pànic.
Avi: Però no volia marxar sense fer les paus...
Avi: Li encantava nedar, i jo he trigat 60 anys a tornar-ho a fer.
Avi: Gràcies per escoltar-me Mila.
do GameState.invitados_fiesta["abuelo"] = true
do NPCManager.npc_states["abuelo"]["next_dialogue"] = "start_rio_bloqueado"
do GameState._on_dialogue_ended()
=> END

~ start_rio_bloqueado
Avi: No et preocupis, arribaré a temps per la teva festa.
do GameState._on_dialogue_ended()
=> END